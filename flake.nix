{
  description = "Zen Browser";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs?ref=nixos-unstable";
  };

  outputs =
    { self, nixpkgs, ... }:
    let
      system = "x86_64-linux";
      pkgs = import nixpkgs {
        inherit system;
      };

      source = import ./source.nix;
    in
    {
      packages."${system}" = rec {
        zen-browser = pkgs.callPackage ./nix/package.nix { inherit (source) src version; };
        update = pkgs.writeShellScriptBin "fetch-source" ''
          PATH="$PATH:${pkgs.jq}/bin:${pkgs.curl}/bin"
          VERSION="$(curl -s https://api.github.com/repos/zen-browser/desktop/releases/latest | jq -r .tag_name)"

          if [ -z "$VERSION" ] || [ "$VERSION" = "null" ]; then
            echo "Failed to fetch latest version."
            exit 1
          fi

          echo "Latest version: $VERSION"

          URL="https://github.com/zen-browser/desktop/releases/download/$VERSION/zen.linux-x86_64.tar.xz"

          echo "Prefeching hash..."
          HASH="$(nix-prefetch-url --quiet --unpack $URL)"

          echo "Hash is \"sha256:$HASH\"."

          cat <<EOF > ./source.nix
          # Generated by "nix run .#update", don't edit this file.
          {
            version = "$VERSION";
            src = fetchTarball {
              url = "$URL";
              sha256 = "sha256:$HASH";
            };
          }
          EOF

          echo "\"source.nix\" has been generated."
        '';
        default = zen-browser;
      };
      homeManagerModules."${system}" = rec {
        zen-browser = import ./nix/hm-module.nix self;
        default = zen-browser;
      };
    };
}
