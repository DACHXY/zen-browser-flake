{
  description = "Zen Browser";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs?ref=nixos-unstable";
  };

  outputs =
    { self, nixpkgs, ... }:
    let
      system = "x86_64-linux";
      pkgs = import nixpkgs {
        inherit system;
      };

      source = import ./source.nix;
    in
    {
      packages."${system}" = rec {
        zen-browser = pkgs.callPackage ./nix/package.nix { inherit (source) src version; };
        update = pkgs.writeShellScriptBin "fetch-source" ''
          PATH="$PATH:${pkgs.jq}/bin:${pkgs.curl}/bin"
          VERSION="$(curl -s https://api.github.com/repos/zen-browser/desktop/releases/latest | jq -r .tag_name)"
          URL="https://github.com/zen-browser/desktop/releases/download/$VERSION/zen.linux-x86_64.tar.xz"
          HASH="$(nix-prefetch-url --unpack $URL)"

          cat <<EOF > ./source.nix
          # Generated by `nix run .#update`, don't edit this file.
          {
            version = "$VERSION";
            src = fetchTarball {
              url = "$URL";
              sha256 = "sha256:$HASH";
            };
          }
          EOF
        '';
        default = zen-browser;
      };
      homeManagerModules."${system}" = rec {
        zen-browser = import ./nix/hm-module.nix self;
        default = zen-browser;
      };
    };
}
